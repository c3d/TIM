MX -> 4
MY -> 0
RX -> -4
RY -> 0
S -> 50
W -> 1250
RX2 -> RX * 50 / S
MX2 -> MX * 50 / S

color "transparent"
if page_number > 3 then
    line_color "black"
    line_width 15
    line_arrow -450, MY * 50, NONE, 450, RY * 50, ARROWHEAD

line_color "gray"
line_width 1

timeline S:integer ->
    locally
        line_color "black"
        color "transparent"
        texture ""
        line_width 12
        translate_y 150
        rotate_x -45
        line_arrow -150 * S, 0, NONE, 150 * S, 0, ARROWHEAD

page "Cubic curvature",
    cubic_curvature false

page "Cubic curvature with line",
    cubic_curvature true

cubic_curvature Line ->
    frame_texture 256, 256,
        color "white"
        line_color "black"
        line_width 4
        rectangle 256, 256
    texture_transform
        scale 10
    texture_wrap true, true
    rotate_z 90
    rotate_x mouse_y * 0.1 + 45
    rotate_y -mouse_x * 0.1 - 45

    for I in 1..3 loop
        locally
            rotate 120 * I, 1, 1, 1
            color_hsv 120 * I, 20%, 100%
            translate_z 150
            rectangle 300, 300
            if Line then
                timeline (if I = 1 then -1 else 1)

page "Grid alone",
    grid false, false

page "No curvature",
    grid false, false

page "Event",
    grid false, false
    color "blue"
    circle 50*RX, 50*RY, 20

adj_line_to X, Y ->
    if (X = RX or X = RX + 1) and (Y = RY or Y = RY + 1) then
        line_to (RX + 0.5) * S, (RY + 0.5) * S
    else
        line_to X * S, Y * S

page "Event direction",
    grid false, false
    color "blue"
    circle 50*RX, 50*RY, 20

    line_color "blue"
    line_width 8
    line_arrow 50*RX - 250, 50*RY + 150, NONE, 50*RX - 25, 50*RY + 25, ARROWHEAD

page "Light speed",
    grid false, false
    color "blue"
    circle 50*RX, 50*RY, 20

    line_color "blue"
    line_width 8
    line_arrow 50*RX - 250, 50*RY + 250, NONE, 50*RX - 25, 50*RY + 25, ARROWHEAD

page "Receive particle",
    grid false, true

page "Non commutative",
    grid false, true

    line_color "darkgreen"
    line_width 8
    line_arrow 50*RX + 25, 50*RY - 25, NONE, 50*RX + 25, 50*RY + 75, ARROWHEAD
    line_arrow 50*RX + 25, 50*RY + 75, NONE, 50*RX - 75, 50*RY + 75, ARROWHEAD
    line_arrow 50*RX - 75, 50*RY + 75, NONE, 50*RX - 25, 50*RY - 25, ARROWHEAD
    line_color "orange"
    line_arrow 50*RX - 25, 50*RY - 25, NONE, 50*RX + 75, 50*RY - 25, ARROWHEAD

page "Receive particle arrow",
    grid false, true, false, true

page "Emit particle",
    grid true, false

page "Emit particle arrow",
    grid true, false, true, false

page "Receive then emit",
    grid true, true

page "Receive then emit arrow",
    grid true, true, true, true

page "Grid scale",
    on "pageentry",
        S := 100
    on "pageexit",
        S := 50
    grid true, true, true, true

D -> 0
TOPX -> 0
BOTX -> 0
TOP -> 14
BOT -> 10

page "Invention of mass",
    on "key:s",
        S := 150 - S
        writeln "Changed S=", S
        refresh 0
    for X in -25..25 loop
        line -W, X * S, W, X * S
        line X * S, W, X * S, S
        line X * S, -W, X * S, 0

    D := 0
    TOPX := -10
    BOTX := -10
    while TOPX < 25 and BOTX < 25 loop
        line TOPX * S, S, BOTX * S, 0
        if D >= 0 then
            TOPX := TOPX + 1
            D := D - BOT
        if D < 0 then
            BOTX := BOTX + 1
            D := D + TOP
        else
            locally
                color "blue"
                circle BOTX * S, 0, 10

page "Definition of mass",
    grid true, true, true, true

    locally
        line_color "brown"
        line_width 18
        line_arrow MX * 50, MY * 50, ARROWHEAD, RX * 50, RY * 50, ARROWHEAD

    line_width 0
    text_box 0, 70, 200, 75,
        font "Times", 120, italic
        color "brown"
        align_center
        vertical_align_center
        text "m"

page "Double frequency",
    grid true, true, true, true
    locally
        translate 25, 25, 0
        grid false, false

    locally
        line_color "brown"
        line_width 18
        line_arrow MX * 50, MY * 50, ARROWHEAD, RX * 50, RY * 50, ARROWHEAD

    line_width 0
    text_box 0, 70, 200, 75,
        font "Times", 120, italic
        color "brown"
        align_center
        vertical_align_center
        text "m"

grid Emit, Receive, EmitArrow, ReceiveArrow ->
    for X in -25..25 loop
        if (not Receive or X >= RX2) and (not Emit or X <= MX2) then
            line X * S, -W, X * S, W
        if Receive and X  <= RX2 then
            path
                move_to X * S, -W
                line_to X * S, RY * S
                line_to (X-1) * S, (RY+1) * S
                line_to (X-1) * S, W
        if Emit and X >= MX2 then
            path
                move_to X * S, W
                line_to X * S, MY* S
                line_to (X+1) * S, (MY-1) * S
                line_to (X+1) * S, -W
        line -W, X * S, W, X * S

    if ReceiveArrow then locally
        line_color "blue"
        line_width 8
        line_arrow 50*RX - 250, 50*RY + 250, NONE, 50*RX - 25, 50*RY + 25, ARROWHEAD

    if Receive then
        color "blue"
        circle 50*RX, 50*RY, 20

    if EmitArrow then locally
        line_color "red"
        line_width 8
        line_arrow 50*MX + 25, 50*MY - 25, NONE, 50*MX + 250, 50*MY - 250, ARROWHEAD
    if Emit then
        color "red"
        circle 50*MX, 50*MY, 20


grid Emit, Receive -> grid Emit, Receive, false, false
