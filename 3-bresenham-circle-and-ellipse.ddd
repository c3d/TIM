// Démonstration de l'algorithme de cercles de Bresenham en Tao3D
// Cas général capable de tracer des ellipses

// Débranche le lissage de l'affichage
texture_mag_filter "nearest"

// On dessine dans une texture de 32x32 pixels afin de bien voir le résultat
frame_texture 32, 32,

    color "black"
    draw_ellipse 0, 0, 15, 15

    color "red"
    draw_ellipse 0, 0, 15, 8

    color "blue"
    draw_ellipse 5, 5, 3, 10

// Affiche la texture dans un rectangle plus grand (zoom d'un facteur 16)
color "white"
rectangle 0, 0, 32*16, 32*16

// La fonction qui dessine une ellipse
draw_ellipse xc, yc, a, b ->
    a2 := a*a
    b2 := b*b
    d := 0

    x  := a
    y  := 0
    sx := NegSign x
    sy := NegSign y
    finished := 0

    while finished = 0 loop
        plot xc + x, yc + y
        s  := NegSign d

        dx := d + b2 * (2*sx*x + 1)
        dy := d + a2 * (2*sy*y + 1)
        if Abs dy <= Abs dx then
            y := y + sy
            d := dy
        else
            x := x + sx
            d := dx

        if x = 0 then sy := -sy
        if y = 0 then sx := -sx

        if x = a and y = 0 then finished := 1

// La fonction qui dessine un pixel aux coordonnées x, y
plot x, y ->
    // En Tao3D, les coordonnées sont au centre des pixels donc ajouter 0.5
    rectangle x + 0.5, y + 0.5, 1, 1

Abs X -> if X < 0 then -X else X
NegSign X -> if X > 0 then -1 else 1

key "Space" -> refresh 0
